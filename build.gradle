plugins {
    id 'java'
    id 'application'
    id 'org.javamodularity.moduleplugin' version '1.8.12'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'org.beryx.jlink' version '3.0.1'
}

group 'org.example'
version '1.0-SNAPSHOT'

project.description = "Simple tool for tracking your finances"

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.10.0'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'org.example.demo'
    mainClass = 'org.example.demo.ExpenseApplication'
}

javafx {
    version = '21'
    modules = ['javafx.controls', 'javafx.fxml']
}

dependencies {
    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")

    implementation group: 'org.xerial', name: 'sqlite-jdbc', version: '3.45.1.0'
    implementation("org.apache.logging.log4j:log4j-core:2.22.1")
    implementation("org.slf4j:slf4j-log4j12:2.0.11")
    implementation("org.slf4j:slf4j-api:2.0.11")

}

test {
    useJUnitPlatform()
}

jlink {
    imageZip = project.file("${buildDir}/distributions/app-${javafx.platform.classifier}.zip")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'ExpenseApp'
    }
    addExtraDependencies('slf4j')
    jpackage {
        installerOptions = [
                '--description', project.description,
                '--copyright', 'None'
        ]
        installerType = project.findProperty('installerType')
        if (org.gradle.internal.os.OperatingSystem.current().linux) {
            installerType = "deb"
            imageOptions += ['--icon', 'src/main/resources/org/example/demo/assets/icon.png']
            installerOptions += [
                    '--linux-menu-group', 'Office',
                    '--linux-shortcut'
                    ]
        }
        if (org.gradle.internal.os.OperatingSystem.current().windows) {
            imageOptions += ['--icon', 'src/main/resources/org/example/demo/assets/icon.ico']
            installerOptions += [
                    '--win-per-user-install', '--win-dir-chooser',
                    '--win-menu', '--win-shortcut'
            ]
        }
        if (org.gradle.internal.os.OperatingSystem.current().macOsX) {
            imageOptions += ['--icon', 'src/main/resources/org/example/demo/assets/icon.icns']
            installerOptions += [
                    '--mac-package-name', 'FinanceTracker',
                    '--mac-package-identifier', 'org.example.demo',
                    '--mac-package-signing-prefix', 'org.example.demo'
            ]
        }
    }
}

jlinkZip {
    group = 'distribution'
}